# 如何使用逻辑条件判断

* #if #else
由宏来实现，被称作静态分支，在编译期决定，没有性能损失

* if else语句
但是if条件中仅有uniform变量和常量组成，在运行期相当于 #if #else，也没有运行期损失(但是驱动判断static uniform branch有轻微消耗)

* 真正的if else语句，也就是动态分支

在CPU编程中,if, else都会进行优化，CPU最终只会执行其中一个分支。但这种一会对GPU来说完全无效，因为在片元着色器操作对象为像素, 如果一部分像素采取一个分支，其他采取另一部分，GPU会让所有像素都运行所有分支，最后将选用分支写入寄存器中，所以switch, if , else等在shader中开销不小，应尽量避免使用过多逻辑防止大开销。

使用step()、ceil()、clamp()、lerp()等函数进行替换，用阶梯函数的思维来构建条件语句

| 函数 | 定义 | 解释 |
| :-----| :---- | :---- |
| step(a, b)     | b小于a返回0，否则，返回1 | 结果非0，即1 |
| ceil(x)        | 对输入参数向上取整。| ceil(float(1.3)) ，其返回值为 2.0 |
| floor(x)       | 对输入参数向下取整 | 例如：floor(float(1.3)) ，其返回值为 1.0 |
| clamp(x, a, b) | 如果 x 值小于 a，则返回 a；如果 x 值大于 b，返回 b；否则，返回 x | 定义上下边界，过界责钳制与上下界 |
| [lerp(a, b, w)](Pages/1.0.1Lerp.md) | lerp(a, b, w) = a + (b - a) * w = (1 - w) * a + w * b | 简单来说，lerp函数就是在y1和y2之间过渡，唯一不同的地方就是，y1和y2可以是一个值，也可以是一个函数。 |

## 条件判断
```HLSL
// 等于
float _equal(float x, float y)
{
    return 1.0 - abs(sign(x - y));
}
// 不等于
float _notEqual(float x, float y)
{
    return abs(sign(x - y));
}
// 大于
float _greater(float x, float y)
{
    return max(sign(x - y), 0);
}
// 小于
float _less(float x, float y)
{
    return max(sign(y - x), 0);
}
// 大于等于
float _greaterOrEqual(float x, float y)
{
    return 1 - _less(x, y);
}
// 小于等于
float _lessOrEqual(float x, float y)
{
    return 1 - _greater(x, y);
}
```

## 逻辑运算符
```HLSL
// 且
float _and(float x, float y)
{
    return _greater(x * y, 0);
}
// 或
float _or(float x, float y)
{
    return min(x + y, 1);
}

// if-else
float3 _branch(float condition, float3 trueValue, float3 elseValue)
{
    return lerp(elseValue, trueValue, condition);
}
```

## 案例Shader

```HLSL
Shader "MyCustom/Chapter15_LogicalStatements"
{
    Properties
    {
        _NumRamp        ("_NumRamp",        Range(1, 10))   = 3
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" }
        LOD 100

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex       : POSITION;
                float2 uv           : TEXCOORD0;
            };

            struct v2f
            {
                float4 vertex       : SV_POSITION;
                float2 uv           : TEXCOORD0;
                float4 worldPos     : TEXCOORD1;
            };

            float _NumRamp;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = v.uv;
                o.worldPos = mul(unity_ObjectToWorld, v.vertex);
                return o;
            }
            // 灰阶渐变
            float _rampColor(float u)
            {
                float ramp = 0;

                //if (u <= 0.25)
                //    ramp = 0.25;
                //else if (u > 0.25 && u <= 0.5)
                //    ramp = 0.5;
                //else if (u > 0.5 && u <= 0.75)
                //    ramp = 0.75;
                //else
                //    ramp = 1;

                // 替换
                ramp = floor(u * _NumRamp) / (_NumRamp - 1);

                return ramp;
            }

            // 条件判断
            // 等于
            float _equal(float x, float y)
            {
                return 1.0 - abs(sign(x - y));
            }
            // 不等于
            float _notEqual(float x, float y)
            {
                return abs(sign(x - y));
            }
            // 大于
            float _greater(float x, float y)
            {
                return max(sign(x - y), 0);
            }
            // 小于
            float _less(float x, float y)
            {
                return max(sign(y - x), 0);
            }
            // 大于等于
            float _greaterOrEqual(float x, float y)
            {
                return 1 - _less(x, y);
            }
            // 小于等于
            float _lessOrEqual(float x, float y)
            {
                return 1 - _greater(x, y);
            }

            // 逻辑运算符
            // 且
            float _and(float x, float y)
            {
                return _greater(x * y, 0);
            }
            // 或
            float _or(float x, float y)
            {
                return min(x + y, 1);
            }

            // if-else
            float3 _branch(float condition, float3 trueValue, float3 elseValue)
            {
                return lerp(elseValue, trueValue, condition);
            }

            float3 _stripeColor(float y)
            {
                // 标量取余
                float v = frac(y);

                float3 col = 0;

                //// 1st version
                //if (v > 0 && v <= 0.5)
                //    col = float3(1, 0, 0);
                //else
                //    col = float3(0, 1, 0);

                //// 2nd version
                //if (_greater(v, 0) && _lessOrEqual(v, 0.5))
                //    col = float3(1, 0, 0);
                //else
                //    col = float3(0, 1, 0);

                //// 3rd version
                //if (_and(_greater(v, 0),_lessOrEqual(v, 0.5)))
                //    col = float3(1, 0, 0);
                //else
                //    col = float3(0, 1, 0);

                // 4th version
                col = _branch(_and(_greater(v, 0),_lessOrEqual(v, 0.5)), float3(1, 0, 0), float3(0, 1, 0));
                
                return col;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                float u = i.uv.x;

                //float ramp = _rampColor(u);
                //return ramp;

                float3 stripeColor = _stripeColor(i.worldPos.y);
                return float4(stripeColor, 1);
            }
            ENDCG
        }
    }
}
```

用y轴（垂直方向）取余形成条纹，后进行条件判断进行以单位距离填入不同向量。
<br>![image](https://user-images.githubusercontent.com/74708198/236283416-08d645fc-bc32-4767-984a-8f664d130ebc.png)
