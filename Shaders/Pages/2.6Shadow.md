# 阴影
## Unity中的平行光阴影

### Unity内建Shadow实现方式
含阴影的shader，这里的阴影包含接收阴影和产生阴影：
```HLSL
Shader "MyCustom/Chapter27_PhongWithBuildinShadow"
{
    Properties
    {
        _MainTex            ("Texture",             2D)             = "white" {}
        _Diffuse            ("_Diffuse",            Color)          = (0.5, 0.5, 0.5, 1)
        _SpecularPower      ("_SpecularPower",      Range(0, 150))  = 20
        _SpecularIntensity  ("_SpecularIntensity",  Range(0, 10))   = 1.5
    }
    SubShader
    {
        CGINCLUDE
        float _specular(float3 viewDir, float3 lightDir, float3 normal, float power, float intensity)
        {
            float3 _viewDir = normalize(viewDir);
            float3 _lightDir = normalize(lightDir);
            float3 _normal = normalize(normal);

            float3 halfVector = normalize(_lightDir + _viewDir);
            float hdotn = max(0, dot(halfVector, _normal));
            float specular = pow(hdotn, power) * intensity;
            return specular;
        }
        ENDCG

        Tags { "RenderType"="Opaque" }
        LOD 100

        Pass
        {
            Tags {"LightMode"="ForwardBase"}

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma multi_compile_fwdbase

            #include "UnityCG.cginc"
            #include "Lighting.cginc"
            #include "AutoLight.cginc"

            // 使用unity内建的阴影，需要:
            // SHADOW_COORDS(): 声明阴影纹理坐标
            // TRANSFER_SHADOW: 将顶点从模型空间转换到光源空间
            // UNITY_LIGHT_ATTENUATION: 对阴影贴图进行采样
            // 通过调用fallback "specular"会查找到内建的pass

            struct appdata
            {
                float4 vertex       : POSITION;
                float2 uv           : TEXCOORD0;
                float3 normal       : NORMAL;
            };

            struct v2f
            {
                float2 uv           : TEXCOORD0;
                float3 worldNormal  : TEXCOORD1;  
                float3 worldPos     : TEXCOORD2;  
                float4 pos       : SV_POSITION;
                SHADOW_COORDS(4)
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;

            float4 _Diffuse;
            float _SpecularPower;
            float _SpecularIntensity;

            v2f vert (appdata v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);
                o.worldNormal = UnityObjectToWorldNormal(v.normal);
                o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

                TRANSFER_SHADOW(o);

                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                float3 lightDir = UnityWorldSpaceLightDir(i.worldPos);
                float3 viewDir = UnityWorldSpaceViewDir(i.worldPos);
                float3 specular = _LightColor0.rgb * _specular(viewDir, lightDir, i.worldNormal, _SpecularPower, _SpecularIntensity);

                float3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;

                float3 diffuse = _LightColor0.rgb * _Diffuse.rgb * max(0, dot(i.worldNormal, lightDir));

                UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);

                float4 finalColor = 1;
                finalColor.rgb = ambient + (diffuse + specular) * atten;

                return finalColor;
            }
            ENDCG
        }

        Pass
        {
            Tags {"LightMode"="ForwardAdd"}
            Blend One One

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma multi_compile_fwdadd

            #include "UnityCG.cginc"
            #include "Lighting.cginc"
            #include "AutoLight.cginc"

            struct appdata
            {
                float4 vertex       : POSITION;
                float2 uv           : TEXCOORD0;
                float3 normal       : NORMAL;
            };

            struct v2f
            {
                float2 uv           : TEXCOORD0;
                float3 worldNormal  : TEXCOORD1;  
                float3 worldPos     : TEXCOORD2;  
                float4 vertex       : SV_POSITION;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;

            float4 _Diffuse;
            float _SpecularPower;
            float _SpecularIntensity;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);
                o.worldNormal = UnityObjectToWorldNormal(v.normal);
                o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                float3 lightDir = UnityWorldSpaceLightDir(i.worldPos);
                float3 viewDir = UnityWorldSpaceViewDir(i.worldPos);
                float3 specular = _LightColor0.rgb * _specular(viewDir, lightDir, i.worldNormal, _SpecularPower, _SpecularIntensity);

                float3 diffuse = _LightColor0.rgb * _Diffuse.rgb * max(0, dot(i.worldNormal, lightDir));
                
                float atten = 1.0;
                #if defined (POINT)
                    float3 lightCoord = mul(unity_WorldToLight, float4(i.worldPos, 1)).xyz; 
                    atten = tex2D(_LightTexture0, dot(lightCoord, lightCoord).rr).UNITY_ATTEN_CHANNEL;
                #endif
                float4 finalColor = 1;
                finalColor.rgb = (diffuse + specular) * atten;

                return finalColor;
            }
            ENDCG
        }

        //Pass
        //{
        //    Tags {"LightMode"="ShadowCaster"}
        //}
    }
    Fallback "Specular" // 使用内建的pass
}
```
在Pass中增加：
* SHADOW_COORDS(): 声明阴影纹理坐标
* TRANSFER_SHADOW: 将顶点从模型空间转换到光源空间
* UNITY_LIGHT_ATTENUATION: 对阴影贴图进行采样
* 通过调用fallback "specular"会查找到内建的pass, 也可以自己写
* 注: 在多光源处理pass中没有加入这些，所以点光源无法产生阴影。
<br>![image](https://user-images.githubusercontent.com/74708198/233797155-bd93e6e6-aeb1-40f4-b7bd-b4d37c926802.png)

### 自定义shadow
ShadowMap的实现主要分为两个pass:

<br>**第一个pass：** 在光源位置创建一个深度摄像机，以光源位置作为视点，将每个像素点的深度值（z-depth）也就是距离光源最近的对象距离记录在 Z-buffer 中，生成深度图
<br>**第二个pass：** 从正常摄像机渲染场景，将每个fragment 到光源的距离和 Shadow Map 中保存的深度值进行比较，如果大于后者则说明被其他物体遮挡处于阴影之中
<br>![image](https://user-images.githubusercontent.com/74708198/233792279-48a2f86d-d01b-4694-a252-573842a246e4.png)

### 

### Shadow Map的问题以及优化方案

Shadow Acne: 阴影的产生和深度纹理的像素是相关的。深度纹理像素值越高，shadow acne的效果相对来说越小，然而shadow acne不可能通过提高像素来完全消除。因为只要光线和法向量的夹角不是90度，永远会有一部分的geometry会被判定处在阴影当中。这个问题可以通过给深度值加一个小小的bias来解决。通过法向量的和光线的夹角来判断bias要更为准确:
<br>float bias = max(max_bias * (1.0 - dot(normal, lightDir)), min_bias); 
<br>![image](https://user-images.githubusercontent.com/74708198/233792348-6137cd07-6fa7-4a1f-9803-998792032610.png) ![image](https://user-images.githubusercontent.com/74708198/233792353-0b843957-4493-443d-b592-53abf09ae89b.png)

阴影边缘抗锯齿: PCF(Percentage Closer Filtering)通过对附近像素多次采样求平均来实现阴影边缘抗锯齿，达到软阴影的效果
<br>![image](https://user-images.githubusercontent.com/74708198/233792383-dca1dc21-1ec4-4e3e-9921-5a2ec0a25e93.png) ![image](https://user-images.githubusercontent.com/74708198/233792385-7dca4441-243d-4af1-8f84-3487bde48dc3.png)




