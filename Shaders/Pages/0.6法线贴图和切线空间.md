# Normal Map and Tangent Space

## Normal Map
![image](https://user-images.githubusercontent.com/74708198/223919292-27fa9c71-e0b5-4916-9f74-d75073a6defb.png)


## Tangent Space and TBN Matrix
在几何着色器手动计算切线和副切线，且画出。
```HLSL
Shader "MyCustom/CalTBN"
{
	Properties
	{
		_LineLength				("LineLength",				float) = 0.03
		_NormalColor			("Normal LineColor",		COLOR) = (0, 0, 1, 1)
		_TangentColor			("Tangent LineColor",		COLOR) = (1, 0, 0, 1)
		_BitangentColor			("Bitangent LineColor",		COLOR) = (0, 1, 0, 1)

		[Toggle]_ShowNormal		("Show Normal",				Range(0 , 1)) = 1
		[Toggle]_ShowTangent	("Show Tangent",			Range(0 , 1)) = 1
		[Toggle]_ShowBitangent	("Show Bitangent",			Range(0 , 1)) = 1
	}
	SubShader
	{
		Pass
		{
			Tags { "RenderType" = "Opaque" }
			LOD 200

			CGPROGRAM
				#pragma target 5.0
				#pragma vertex VS_Main
				#pragma fragment FS_Main
				#pragma geometry GS_Main
				#include "UnityCG.cginc"

				float _LineLength;
				fixed4 _NormalColor;
				fixed4 _TangentColor;
				fixed4 _BitangentColor;

				fixed _ShowNormal;
				fixed _ShowTangent;
				fixed _ShowBitangent;

				struct GS_INPUT
				{
					float4 pos			:POSITION;
					float2 uv			:TEXCOORD0;
					float3 normal		:NORMAL;
					float3 tangent		:TANGENT;
				};
				struct FS_INPUT
				{
					float4 pos			:POSITION;
					float4 color		:fixed4;
				};

				GS_INPUT VS_Main(appdata_tan v)
				{
					GS_INPUT output = (GS_INPUT)0;
					output.pos = mul(unity_ObjectToWorld, v.vertex);
					float3 worldNormal = UnityObjectToWorldNormal(v.normal);
					float3 worldTangent = UnityObjectToWorldDir(v.tangent.xyz);
					output.normal = normalize(worldNormal);
					output.tangent = worldTangent;
					output.uv = v.texcoord;
					return output;
				}

				// 几何着色器
				[maxvertexcount(6)]
				void GS_Main(triangle GS_INPUT p[3], inout LineStream<FS_INPUT> lineStream)
				{
					for (int i = 0; i < 3; i++) 
					{
						float4 pos = mul(UNITY_MATRIX_VP, p[i].pos);

						// TODO 计算出切线和副切线
						//p2，p3的索引号
						int p2Index = (i + 1) % 3;
						int p3Index = (i + 2) % 3;

						//定义在任意一个三角面上的p1，p2，p3
						float3 p1 = p[i].pos;
						float3 p2 = p[p2Index].pos;
						float3 p3 = p[p3Index].pos;

						float2 uv1 = p[i].uv;
						float2 uv2 = p[p2Index].uv;
						float2 uv3 = p[p3Index].uv;

						float3 e1 = p2 - p1;
						float3 e2 = p3 - p1;

						// [0] = u
						float t1 = uv2[0] - uv1[0];
						float t2 = uv3[0] - uv1[0];
						// [1] = v
						float b1 = uv2[1] - uv1[1];
						float b2 = uv3[1] - uv1[1];

						float f = 1.0 / (t1 * b2 - t2 * b1);

						float3 tangent = f * (b2 * e1 - b1 * e2);
						tangent = normalize(tangent - dot(tangent, p[i].normal) * p[i].normal);

						float3 bitangent = f * (-t2 * e1 + t1 * e2);
						//bitangent = normalize(bitangent - dot(bitangent, p[i].normal) * p[i].normal - dot(bitangent, tangent) * tangent);
						bitangent = cross(tangent, p[i].normal);

						if (_ShowNormal > 0.5)
						{
							// 画出线段
							//draw normal
							FS_INPUT pIn_normal;
							pIn_normal.pos = pos;
							pIn_normal.color = _NormalColor;
							lineStream.Append(pIn_normal);

							FS_INPUT pIn1_normal;
							pIn1_normal.pos = mul(UNITY_MATRIX_VP, p[i].pos + float4(p[i].normal, 0) *( _LineLength));
							pIn1_normal.color = _NormalColor;
							lineStream.Append(pIn1_normal);
							lineStream.RestartStrip();
						}

						if (_ShowTangent > 0.5)
						{
							//draw hand calculator tangent
							FS_INPUT pIn_calTangent;
							pIn_calTangent.pos = pos;
							pIn_calTangent.color = _TangentColor;
							lineStream.Append(pIn_calTangent);

							FS_INPUT pIn1_calTangent;
							pIn1_calTangent.pos = mul(UNITY_MATRIX_VP, p[i].pos + float4(tangent, 0) *( _LineLength));
							pIn1_calTangent.color = _TangentColor;
							lineStream.Append(pIn1_calTangent);
							lineStream.RestartStrip();
						}

						if (_ShowBitangent > 0.5)
						{
							//draw hand calculator bitangent
							FS_INPUT pIn_calBitangent;
							pIn_calBitangent.pos = pos;
							pIn_calBitangent.color = _BitangentColor;
							lineStream.Append(pIn_calBitangent);

							FS_INPUT pIn1_calBitangent;
							pIn1_calBitangent.pos = mul(UNITY_MATRIX_VP, p[i].pos + float4(bitangent, 0) * (_LineLength));
							pIn1_calBitangent.color = _BitangentColor;
							lineStream.Append(pIn1_calBitangent);
							lineStream.RestartStrip();
						}
					}
				}

				fixed4 FS_Main(FS_INPUT input) : COLOR
				{
					return input.color;
				}
			ENDCG
		}
	}
}
```
<br>![image](https://user-images.githubusercontent.com/74708198/223934262-a91717f9-1044-4c59-869e-1cfdd16ac6d4.png)



<br>BlinnPhong模型，在切线空间中计算光照。

```HLSL
Shader "MyCustom/NormalMapW2T"
{
    Properties
    {
        _MainTex            ("Texture",             2D) = "white" {}
        _NormalTex          ("Texture",             2D) = "bump"  {}
        _SpecularColor      ("Specular Color",      color) = (1.0, 1.0, 1.0, 1.0)
        _Shininess          ("Shininess",           float) = 20.0  
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" }
        LOD 100

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            // make fog work
            #pragma multi_compile_fog

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex           : POSITION;
                float2 uv               : TEXCOORD0;
                float3 normal           : NORMAL;
                float4 tangent          : TANGENT;
            };

            struct v2f
            {
                float2 uv               : TEXCOORD0;
                float4 vertex           : SV_POSITION;
                float3 viewDir          : TEXCOORD1;
                float3 lightDir         : TEXCOORD2;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;

            sampler2D _NormalTex;
            float4 _NormalTex_ST;

            float4 _SpecularColor;
            float _Shininess;

            uniform float4 _LightColor0;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);

                // tbn
                // | tangent.x      tangent.y       tangent.z |
                // | bitangent.x    bitangent.y     bitangent.z|
                // | normal.x       normal.y        ormal.z|
                float3 bitangent = cross(v.normal, v.tangent.xyz) * v.tangent.w;
                float3x3 tbn = float3x3(v.tangent.xyz, bitangent, v.normal);

                // 将 摄像机->顶点的方向向量从世界空间转换到模型空间坐标
                float3 viewDirOS = mul(unity_WorldToObject, float4(_WorldSpaceCameraPos.xyz, 1)).xyz - v.vertex.xyz;
                // 将 灯光方向向量从世界坐标转换到模型空间坐标
                float3 lightDirOS = mul(unity_WorldToObject, float4(_WorldSpaceLightPos0.xyz, 0)).xyz;

                // 从模型空间统一变换到切线空间
                o.viewDir = normalize(mul(tbn, viewDirOS));
                o.lightDir = normalize(mul(tbn, lightDirOS));

                return o;
            }

            float3 blinnPhongSpecular(float3 normal, float3 viewDir, float3 lightDir)
            {
                float3 halfAngle = normalize(viewDir + lightDir);
                float3 specular = _LightColor0.rgb * _SpecularColor.rgb * pow( saturate(dot(halfAngle, normal)), _Shininess);
                return specular;
            }

            float4 frag (v2f i) : SV_Target
            {
                float4 col = tex2D(_MainTex, i.uv);
                float3 normal = UnpackNormal(tex2D(_NormalTex, i.uv));

                float3 viewDir = i.viewDir;
                float3 lightDir = i.lightDir;

                float3 diffuse = _LightColor0.rgb * saturate(dot(normal, lightDir) * 0.5 + 0.5);
                float3 specular = blinnPhongSpecular(normal, viewDir, lightDir);
                float3 ambient = UNITY_LIGHTMODEL_AMBIENT.rgb;

                return float4((diffuse + specular + ambient) * col, 1);
            }
            ENDCG
        }
    }
}
```

<br>BlinnPhong模型，用TBN矩阵将解码出的normal换算到世界空间计算。
```HLSL
Shader "MyCustom/NormalMapT2W"
{
    Properties
    {
        _MainTex            ("Texture",             2D) = "white" {}
        _NormalTex          ("Texture",             2D) = "bump"  {}
        _SpecularColor      ("Specular Color",      color) = (1.0, 1.0, 1.0, 1.0)
        _Shininess          ("Shininess",           float) = 20.0  
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" }
        LOD 100

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            // make fog work
            #pragma multi_compile_fog

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex           : POSITION;
                float2 uv               : TEXCOORD0;
                float3 normal           : NORMAL;
                float4 tangent          : TANGENT;
            };

            struct v2f
            {
                float2 uv               : TEXCOORD0;
                float4 vertex           : SV_POSITION;
                float3 tbn[3]           : TEXCOORD1;
                float3 viewDir          : TEXCOORD4;
                float3 lightDir         : TEXCOORD5;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;

            sampler2D _NormalTex;
            float4 _NormalTex_ST;

            float4 _SpecularColor;
            float _Shininess;

            uniform float4 _LightColor0;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                float4 worldPos = mul(unity_ObjectToWorld, v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);

                // 世界空间下的V，L向量
                o.viewDir = normalize(_WorldSpaceCameraPos.xyz - worldPos.xyz);
                o.lightDir = normalize(_WorldSpaceLightPos0.xyz);

                // tbn
                float3 bitangent = cross(v.normal, v.tangent.xyz) * v.tangent.w;

                o.tbn[0] = normalize(mul((float3x3)unity_ObjectToWorld, v.tangent.xyz));    // T
                o.tbn[1] = normalize(mul((float3x3)unity_ObjectToWorld, bitangent));        // B
                o.tbn[2] = normalize(mul((float3x3)unity_ObjectToWorld, v.normal));         // N

                return o;
            }

            float3 blinnPhongSpecular(float3 normal, float3 viewDir, float3 lightDir)
            {
                float3 halfAngle = normalize(viewDir + lightDir);
                float3 specular = _LightColor0.rgb * _SpecularColor.rgb * pow( saturate(dot(halfAngle, normal)), _Shininess);
                return specular;
            }

            float4 frag (v2f i) : SV_Target
            {
                float4 col = tex2D(_MainTex, i.uv);

                // 对于法线贴图的处理
                float3x3 tbn = float3x3(i.tbn[0], i.tbn[1], i.tbn[2]); //建立TBN矩阵
                float3 normal = UnpackNormal(tex2D(_NormalTex, i.uv)); // 采样解码 normalMap，获取nDirTS
                normal = normalize(mul(normal, tbn)); //法线贴图解码后的 nDirTS -> nDirWS

                float3 viewDir = i.viewDir;
                float3 lightDir = i.lightDir;

                float3 diffuse = _LightColor0.rgb * saturate(dot(normal, lightDir) * 0.5 + 0.5);
                float3 specular = blinnPhongSpecular(normal, viewDir, lightDir);
                float3 ambient = UNITY_LIGHTMODEL_AMBIENT.rgb;

                return float4((diffuse + specular + ambient) * col, 1);
            }
            ENDCG
        }
    }
}

```
