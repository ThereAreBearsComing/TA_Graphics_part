# Computer Shader
#### REF:原文https://zhuanlan.zhihu.com/p/368307575
## Intro
最初的巫毒显卡只有俩计算单元，顶点单元和片元单元。但后边人们需要更通用的硬件，并且当时两单元计算量不成正比，所以2000年后NV和AMD发布了现在这种通用GPU，适合大量矩阵运算的硬件单元。看硬件参数也会法线，与CPU不同，GPU是由大量小核心组成，所以AI，图形等等大量并行运算时，GPU效率明显高于CPU非常之多。

当代GPU被设计成可以执行大规模的并行操作，这有益于图形应用，因为在渲染管线中，不论是顶点着色器还是像素着色器，它们都可以独立进行。然而对于一些非图形应用也可以受益于GPU并行架构所提供的大量计算能力。比如说我们有个应用可以把两个excel里的N个值相加，如果N很大，那么是不是可以利用GPU来进行这些相加计算，来提升速度。像这样的非图形应用使用GPU的情况，我们称之为 **GPGPU**（General Purpose GPU）编程。

对于GPGPU编程而言，用户通常需要将GPU计算后的结果返回到CPU中。例如前面的例子中，我们要在CPU中读取到GPU计算后值，以便可以将结果写入到新的excel中。这就涉及到将数据从GPU显存（Video Memory）中拷贝到CPU系统内存（System Memory）中的操作，该操作非常之**慢**。但是相比使用GPU来计算所提升的运行速度而言，可以忽略此问题。下图展示了CPU和RAM、GPU和VRAM、CPU和GPU之间的相对内存带宽速度（图中的数字只是说明性的数字，以显示带宽之间的数量级差异），可以发现瓶颈在于CPU和GPU之间的内存传输。

<br>![image](https://github.com/ThereAreBearsComing/aBookOFtechArt/assets/74708198/885d8f67-d707-4f93-8812-ed7f3d6e021a)

要实现GPGPU编程，我们就需要一个方法来访问GPU从而实现数据并行算法，这里就需要使用到Compute Shader（后面简称为CS）。CS属于图形API中的一种可编程着色器，它独立于渲染管线之外，但是可以对GPU资源（存放在显存中）进行读取和写入操作。本质上来说，**CS允许我们访问GPU来实现数据并行算法，而不需要绘制任何东西**。

当然了，CS除了适用于非图形应用的GPGPU编程外，我们也可以用它实现很多图形应用中的效果，例如**剔除**，**模糊**等。由于CS可以直接读写GPU资源，使得我们能够将CS的输出直接绑定到渲染管线上。因此对于图形应用，我们通常使用GPU的计算结果作为渲染管道的输入，因此不需要将结果从GPU传输到CPU。例如我们要实现一个模糊效果，可以先用CS模糊一个Texture，然后模糊后的Texture可以直接作为Fragment Shader的输入。

## Unity中默认的Compute Shader
在Project中右键，即可创建出一个CS文件：
生成的文件属于一种 Asset 文件，并且都是以 **.compute 作为文件后缀**的。我们来看下里面的默认内容：
```C#
#pragma kernel CSMain

RWTexture2D<float4> Result;

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    Result[id.xy] = float4(id.x & id.y, (id.x & 15)/15.0, (id.y & 15)/15.0, 0.0);
}
```
本文的主要目的就是让和我一样的**萌新**能够看懂这区区几行代码的含义，学好了基础才能够看更牛逼的代码嘛。

### kernel
然后我们来看看第一行：
```C#
#pragma kernel CSMain
```
CSMain其实就是一个函数，在代码后面可以看到，而 kernel 是内核的意思，这一行即把一个名为CSMain的函数声明为内核，或者称之为核函数。这个核函数就是最终会在GPU中被执行。

一个CS中**至少要有一个kernel才能够被唤起**。声明方法即为：
```C#
#pragma kernel functionName
```
我们也可用它在一个CS里声明多个内核，此外我们还可以再该指令后面定义一些预处理的宏命令，如下：
```C#
#pragma kernel KernelOne SOME_DEFINE DEFINE_WITH_VALUE=1337
#pragma kernel KernelTwo OTHER_DEFINE
```
我们不能把注释写在该命令后面，而应该**换行写注释**，例如下面写法会造成编译的报错：
```C#
#pragma kernel functionName  // 一些注释
```
### RWTexture2D
接着我们再来看看第二行：
```C#
RWTexture2D<float4> Result;
```
看着像是声明了一个和纹理有关的变量，具体来看一下这些关键字的含义。

RWTexture2D中，RW其实是**Read**和**Write**的意思，Texture2D就是二维纹理，因此它的意思就是**一个可以被CS读写的二维纹理**。如果我们只想读不想写，那么可以使用Texture2D的类型。

我们知道纹理是由一个个像素组成的，每个像素都有它的下标，因此我们就可以通过像素的下标来访问它们，例如：Result[uint2(0,0)]。

同样的每个像素会有它的一个对应值，也就是我们要读取或者要写入的值。这个值的类型就被写在了`< >`当中，通常对应的是一个rgba的值，因此是float4类型。通常情况下，我们会在CS中处理好纹理，然后在Fragment Shader中来对处理后的纹理进行采样。

这样我们就大致理解这行代码的意思了，**声明了一个名为Result的可读写二维纹理，其中每个像素的值为float4**。

在CS中可读写的类型除了**RWTexture**以外还有**RWBuffer**和**RWStructuredBuffer**，后面会介绍。

### numthreads
然后是下面一句（很重要！）：
```C#
[numthreads(8,8,1)]
```
又是num，又是thread的，肯定和线程数量有关。没错，它就是定义**一个线程组（Thread Group）中可以被执行的线程（Thread）总数量**。

我们先来看看什么是线程组。在GPU编程中，我们可以将所有要执行的线程划分成一个个线程组，一个线程组在单个流多处理器（Stream Multiprocessor，简称SM）上被执行。如果我们的GPU架构有16个SM，那么至少需要16个线程组来保证所有SM有事可做。为了更好的利用GPU，每个SM至少需要两个线程组，因为SM可以切换到处理不同组中的线程来隐藏线程阻塞（如果着色器需要等待Texture处理的结果才能继续执行下一条指令，就会出现阻塞）。

每个线程组都有一个各自的共享内存（Shared Memory），该组中的所有线程都可以访问改组对应的共享内存，但是不能访问别的组对应的共享内存。因此线程同步操作可以在线程组中的线程之间进行，不同的线程组则不能进行同步操作。

每个`线程组`中又是由n个`线程`组成的，线程组中的线程数量就是通过numthreads来定义的，格式如下：
```C#
numthreads(tX, tY, tZ)
```
* **注：**X，Y，Z前加个t方便和后续线程组的X，Y，Z进行区分。

其中 tX x tY x tZ 的值即线程的总数量，例如 numthreads(4, 4, 1) 和 numthreads(16, 1, 1) 都代表着有16个线程。那么为什么不直接使用 numthreads(num) 这种形式定义，而非要分成tX，tY，tZ这种三维的形式呢？看到后面自然就懂其中的奥秘了。

**每个核函数前面我们都需要定义numthreads**，否则编译会报错。

其中tX，tY，tZ三个值也并不是也可随便乱填的，比如来一刀 tX=99999 暴击一下，这是不行的。它们在不同的版本里有如下的约束：
| Compute Shader 版本 | tZ的最大取值 | 最大线程数量（tX x tY x tZ） |
| :---- | :---- | :----- |
| cs_4_x | 1 | 768 |
| cs_5_0 | 64 | 1024 |

如果是NVIDIA的显卡，线程组中的线程又会被划分成一个个**Warp**，每个Warp由32个线程组成，一个Warp通过SM来调度。在SIMD32下，当SM操控一个Warp执行一个指令，意味着有32个线程同时执行相同的指令。假如我们使用numthreads设置每个线程组只有10个线程，但是由于SM每次调度一个Warp就会执行32个线程，这就会造成有22个线程是不干活的（静默状态），从而在性能上无法达到最优。因此针对NVIDIA的显卡，我们应该将线程组中的线程数设置为32的倍数来达到最佳性能。如果是AMD显卡的话，线程组中的线程则是被划分成一个个由64个线程组成**Wavefront**，那么线程组中的线程数应该设置为64的倍数。因此**建议numthreads值设为64的倍数**，这样可以同时顾及到两大主流的显卡。

在Direct3D12中，可以通过**ID3D12GraphicsCommandList::Dispatch(gX,gY,gZ)**方法创建gX x gY x gZ个**线程组**。**注意顺序，先numthreads定义好每个核函数对应线程组里线程的数量（tX x tY x  tZ），再用Dispatch定义用多少线程组(gX x gY x gZ)来处理这个核函数**。

gX，gY，gZ在不同的版本里有如下的约束：
| Compute Shader 版本 | gX和gY的最大取值 | gZ的最大取值 |
| :---- | :---- | :----- |
| cs_4_x | 65535 | 1 |
| cs_5_0 | 65535 | 65535 |

接着我们用一张示意图来看看线程与线程组的结构，如下图：
![image](https://github.com/ThereAreBearsComing/aBookOFtechArt/assets/74708198/1d14f5f1-f906-4eb0-8ba1-de4138f40310)

上半部分代表的是线程组结构，下半部分代表的是单个线程组里的线程结构。因为他们都是由(X,Y,Z)来定义数量的，因此就像一个三维数组，下标都是从0开始。我们可以把它们看做是表格一样：**有Z个一样的表格，每个表格有X列和Y行**。例如线程组中的(2,1,0)，就是第1个表格的第2行第3列对应的线程组，下半部分的线程也是同理。

搞清楚结构，我们就可以很好的理解下面这些与单个线程有关的参数含义：
| 参数 | 	值类型 | 含义 | 	计算公式 |
| :---- | :---- | :----- | :----- |
| SV_GroupID | int3 | 当前线程所在的线程组的ID，取值范围为(0,0,0)到(gX - 1,gY - 1,gZ - 1) | 无 |
| SV_GroupThreadID | int3 | 当前线程在所在线程组内的ID，取值范围为(0,0,0)到(tX - 1,tY - 1,tZ - 1) | 无 |
| SV_DispatchThreadID | int3 | 当前线程在所有线程组中的所有线程里的ID，取值范围为(0,0,0)到(gX x tX - 1, gY x tY - 1, gZ x tZ - 1) | 假设该线程的SV_GroupID = (a, b, c)，SV_GroupThreadID = (i, j, k) 那么SV_DispatchThreadID = (a x tX + i, b x tY + j, c x tZ + k) |
| SV_GroupIndex | int | 当前线程在所在线程组内的下标，取值范围为0到tX x tY x tZ - 1 | 假设该线程的SV_GroupThreadID = (i, j, k) 那么SV_GroupIndex=k x tX x tY + j x tX + i | 

这里需要注意的是，不管是group还是thread，它们的**顺序都是先X再Y最后Z**，用表格的理解就是先行(X)再列(Y)然后下一个表(Z)，例如我们tX=5，tY=6那么第1个thread的SV_GroupThreadID=(0,0,0)，第2个的SV_GroupThreadID=(1,0,0)，第6个的SV_GroupThreadID=(0,1,0)，第30个的SV_GroupThreadID=(4,5,0)，第31个的SV_GroupThreadID=(0,0,1)。group同理，搞清顺序后，SV_GroupIndex的计算公式就很好理解了。

再举个例子，比如SV_GroupID为(0,0,0)和(1,0,0)的两个group，它们内部的第1个thread的SV_GroupThreadID都为(0,0,0)且SV_GroupIndex都为0，但是前者的SV_DispatchThreadID=(0,0,0)而后者的SV_DispatchThreadID=(tX,0,0)。

好好理解下，它们在核函数里非常的重要。

### 核函数
```C#
void CSMain (uint3 id : SV_DispatchThreadID)
{
    Result[id.xy] = float4(id.x & id.y, (id.x & 15)/15.0, (id.y & 15)/15.0, 0.0);
}
```
最后就是我们声明的核函数了（其实就是Compute Shader函数体），其中参数SV_DispatchThreadID的含义上面已经介绍过了，除了这个参数以外，我们前面提到的几个参数都可以被传入到核函数当中，根据实际需求做取舍即可，完整如下：
```C#
void KernelFunction(uint3 groupId : SV_GroupID,
    uint3 groupThreadId : SV_GroupThreadID,
    uint3 dispatchThreadId : SV_DispatchThreadID,
    uint groupIndex : SV_GroupIndex)
{
    
}
```
Unity默认的核函数体内执行的代码就是为我们Texture中下标为 id.xy 的像素赋值一个颜色，这里也就是最牛逼的地方。

举个例子，以往我们想要给一个 x * y 分辨率的Texture每个像素进行赋值，单线程的情况下，我们的代码往往如下：
```C#
for (int i = 0; i < x; i++)
    for (int j = 0; j < y; j++)
        Result[uint2(x, y)] = float4(a, b, c, d);
```
两个循环，像素一个个的慢慢赋值。那么如果我们要每帧给很多张2048 * 2048的图片进行操作，可想而知会卡死你。

如果使用多线程，为了避免不同的线程对同一个像素进行操作，我们往往使用分段操作的方法，如下，四个线程进行处理：
```C#
void Thread1()
{
    for (int i = 0; i < x/4; i++)
        for (int j = 0; j < y/4; j++)
            Result[uint2(x, y)] = float4(a, b, c, d);
}

void Thread2()
{
    for (int i = x/4; i < x/2; i++)
        for (int j = y/4; j < y/2; j++)
            Result[uint2(x, y)] = float4(a, b, c, d);
}

void Thread3()
{
    for (int i = x/2; i < x/4*3; i++)
        for (int j = x/2; j < y/4*3; j++)
            Result[uint2(x, y)] = float4(a, b, c, d);
}

void Thread4()
{
    for (int i = x/4*3; i < x; i++)
        for (int j = y/4*3; j < y; j++)
            Result[uint2(x, y)] = float4(a, b, c, d);
}
```
这么写不是很蠢么，如果有更多的线程，分成更多段，不就一堆重复的代码。但是如果我们能知道每个线程的开始和结束下标，不就可以把这些代码统一起来了么，如下：
```C#
void Thread(int start, int end)
{
    for (int i = start; i < end; i++)
        for (int j = start; j < end; j++)
            Result[uint2(x, y)] = float4(a, b, c, d);
}
```
那我要是可以开出很多很多的线程是不是就可以一个线程处理一个像素了？
```C#
void Thread(int x, int y)
{
    Result[uint2(x, y)] = float4(a, b, c, d);
}
```
用CPU我们做不到这样，但是用GPU，用CS我们就可以，实际上，前面默认的CS的代码里，核函数的内容就是这样的。

接下来我们来看看CS的妙处，**看 id.xy 的值**。id 的类型为SV_DispatchThreadID，我们先来回忆下SV_DispatchThreadID的计算公式：
* 假设该线程的SV_GroupID=(a, b, c)，SV_GroupThreadID = (i, j, k) 那么SV_DispatchThreadID = (a * tX + i, b * tY + j, c * tZ + k)

首先前面我们使用了[numthreads(8 , 8, 1)]，即tX = 8，tY = 8，tZ = 1 ，且 i 和 j 的取值范围为0到7，而k = 0。那么我们线程组(0,0,0)中所有线程的 SV_DispatchThreadID.xy 也就是 id.xy 的取值范围即为 (0, 0) 到 (7, 7)，线程组(1, 0, 0)中它的取值范围为 (8, 0) 到 (15, 7)，...，线程组(0,1,0)中它的取值范围为 (0,8) 到 (7, 15)，...，线程组(a,b,0)中它的取值范围为(a * 8, b * 8, 0)到(a * 8 + 7, b * 8 + 7, 0)。

<br>![image](https://github.com/ThereAreBearsComing/aBookOFtechArt/assets/74708198/709a01bd-ca08-4d11-b9e3-5746eaeea845)

也就是说我们每个线程组会有64个线程同步处理64个像素，并且不同的线程组里的线程不会重复处理同一个像素，若要处理分辨率为 1024 * 1024 的图，我们只需要dispatch(1024/8, 1024/8, 1)个线程组。

这样就实现了成百上千个线程同时处理一个像素了，若用CPU的方式这是不可能的。是不是很妙？

而且我们可以发现numthreads中设置的值是很值得推敲的，例如我们有4 * 4的矩阵要处理，那么设置numthreads(4,4,1)，那么每个线程的SV_GroupThreadID.xy的值不正好可以和矩阵中每项的下标对应上么。

那么我们在Unity中怎么调用核函数，又怎么dispatch线程组以及使用的RWTexture又怎么来呢？这里就要回到我们C#的部分了。

### C#部分
以往的Vertex & Fragment shader我们都是给它关联到Material上来使用的，但是CS不一样，它是由c#来驱动的。先新建一个monobehaviour脚本，Unity为我们提供了一个ComputeShader的类型用来引用我们前面生成的 .compute 文件：
```C#
public ComputeShader computeShader;
```
![image](https://github.com/ThereAreBearsComing/aBookOFtechArt/assets/74708198/37c058a0-6806-4dde-bf29-78d9f51552f8)在Inspector界面关联.compute文件
此外我们再关联一个Material，因为CS处理后的纹理，依旧要经过FragmentShader采样后来显示。
```C#
public Material material;
```
这个Material我们使用一个Unlit Shader，并且纹理不用设置，如下：
<br>![image](https://github.com/ThereAreBearsComing/aBookOFtechArt/assets/74708198/191b2db4-0602-43c1-ab3d-9fb77609e698)
然后关联到我们的脚本上，并且随便建个Cube也关联上这Material。

接着我们可以将Unity中的RenderTexture赋值到CS中的RWTexture2D上，但是需要注意因为我们是多线程处理像素，并且这个处理过程是无序的，因此我们要将RenderTexture的enableRandomWrite属性设置为true，代码如下：
```C#
RenderTexture mRenderTexture = new RenderTexture(256, 256, 16);
mRenderTexture.enableRandomWrite = true;
mRenderTexture.Create();
```
我们创建了一个分辨率为256 * 256的RenderTexture，首先我们要把它赋值给我们的Material，这样我们的Cube就会显示出它。然后要把它赋值给我们CS中的Result变量，代码如下：
```C#
material.mainTexture = mRenderTexture;
computeShader.SetTexture(kernelIndex, "Result", mRenderTexture);
```
这里有一个kernelIndex变量，即核函数下标，我们可以利用FindKernel来找到我们声明的核函数的下标：
```C#
int kernelIndex = computeShader.FindKernel("CSMain");
```
这样在我们Fragment Shader采样的时候，采样的就是CS处理过后的纹理：
```HLSL
fixed4 frag (v2f i) : SV_Target
{
    // _MainTex 就是被处理后的 RenderTexture
    fixed4 col = tex2D(_MainTex, i.uv);
    return col;
}
```
* 注：此时没有将CS的处理结果从GPU回读到CPU的操作，因为处理结果直接输入到渲染管线中由Fragment Shader进行处理了。
最后就是开线程组和调用我们的核函数了，C#的ComputeShader类提供了Dispatch方法为我们一步到位：
```C#
computeShader.Dispatch(kernelIndex, 256 / 8, 256 / 8, 1);
```
为什么是256/8，前面已经解释过了。来看看效果：
<br>![image](https://github.com/ThereAreBearsComing/aBookOFtechArt/assets/74708198/c3aac115-7e7b-4a64-93aa-c381718b7279)
上图就是我们Unity默认生成的CS代码所能带来的效果，我们也可试下用它处理2048 * 2048的Texture，也是非常快的。

## Example
接下来我们再来看看粒子效果的例子：

首先一个粒子通常拥有颜色和位置两个属性，并且我们肯定是要在CS里去处理这两个属性的，那么我们就可以在CS创建一个struct来存储：
```HLSL
struct ParticleData {
	float3 pos;
	float4 color;
};
```
接着，这个粒子肯定是很多很多的，我们就需要一个像List一样的东西来存储它们，HLSL为我们提供了RWStructuredBuffer类型。

### RWStructuredBuffer
它是一个可读写的buffer，并且我们可以指定buffer中的数据类型为我们自定义的struct类型，不用再局限于int，float这类的基本类型。因此我们可以这么定义我们的粒子数据：
```C#
RWStructuredBuffer<ParticleData> ParticleBuffer;
```
为了有动效，我们可以再添加一个时间相关值，我们可以根据时间来修改粒子的位置和颜色：
```C#
float Time;
```
接着就是怎么在核函数里修改我们的粒子信息了，要修改某个粒子，我们肯定要知道粒子在buffer中的下标，并且这个下标在不同的线程中不能重复，否则就可能导致多个线程修改同一个粒子了。

根据前面的介绍，我们知道一个线程组中SV_GroupIndex是唯一的，但是在不同线程组中并不是，例如每个线程组内有1000个线程，那么SV_GroupID都是0到999。那么我们可以根据SV_GroupID把它叠加上去，例如SV_GroupID=(0,0,0)时是0-999，SV_GroupID=(1,0,0)是1000-1999等等，为了方便我们的线程组就可以是(X,1,1)格式。然后我们就可以根据Time和Index随便的摆布下粒子，CS完整代码：
```C#
#pragma kernel UpdateParticle

struct ParticleData {
	float3 pos;
	float4 color;
};

RWStructuredBuffer<ParticleData> ParticleBuffer;

float Time;

[numthreads(10, 10, 10)]
void UpdateParticle(uint3 gid : SV_GroupID, uint index : SV_GroupIndex)
{
	int pindex = gid.x * 1000 + index;
	
	float x = sin(index);
	float y = sin(index * 1.2f);
	float3 forward = float3(x, y, -sqrt(1 - x * x - y * y));
	ParticleBuffer[pindex].color = float4(forward.x, forward.y, cos(index) * 0.5f + 0.5, 1);
	if (Time > gid.x)
		ParticleBuffer[pindex].pos += forward * 0.005f;
}
```

接下来我们要在C#里给粒子初始化并且传递给CS。我们要传递粒子数据，也就是说要给前面的RWStructuredBuffer<ParticleData>赋值，Unity为我们提供了ComputeBuffer类来与RWStructuredBuffer或StructuredBuffer相对应。

### ComputeBuffer

在CS中经常需要将我们一些CPU种自定义的Struct数据读写到显存中，ComputeBuffer就是为这种情况而生的。我们可以在C#里创建并填充它，然后传递到CS或者其他Shader中使用。通常我们用下面方法来创建它：
```C#
ComputeBuffer buffer = new ComputeBuffer(int count, int stride)
```
其中count代表我们buffer中元素的数量，而stride指的是每个元素占用的空间（字节），例如我们传递10个float的类型，那么count=10，stride=4。需要注意的是ComputeBuffer中的stride大小必须和RWStructuredBuffer中每个元素的大小一致。

声明完成后我们可以使用SetData方法来填充，参数为自定义的struct数组：
```C#
buffer.SetData(T[]);
```
最后我们可以使用ComputeShader类中的SetBuffer方法来把它传递到CS中：
```C#
public void SetBuffer(int kernelIndex, string name, ComputeBuffer buffer)	
```
记得用完后把它Release()掉。
	
在C#中我们定义一个一样的Struct，这样才能保证和CS中的大小一致：
```HLSL
public struct ParticleData
{
    public Vector3 pos;//等价于float3
    public Color color;//等价于float4
}
```
然后我们在Start方法中声明我们的ComputeBuffer，并且找到我们的核函数：
