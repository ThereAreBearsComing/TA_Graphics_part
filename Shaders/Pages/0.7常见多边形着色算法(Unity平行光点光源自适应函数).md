# Illumination Models

## 三种着色方式的区别
<img width="114" alt="image" src="https://user-images.githubusercontent.com/74708198/223992711-707f838f-d5f1-4077-a0f0-26ed467f5666.png">

* Flat
  * 一个三角面用一个颜色
  * 每个多边形中只有多边形的面存在法线向量，而其各个顶点没有
  * 在vertex阶段处理光照数据
  * 开销一个面一次

<img width="114" alt="image" src="https://user-images.githubusercontent.com/74708198/223992747-fdc56658-3317-4396-bd02-5cc20287e9f8.png">

* Gouraud
  * 着色时不是对向量进行插值
  * 先计算多边形每个顶点的光照，再通过 **双线性插值** 计算三角形区域中
  * 其它像素的颜色
  * 其多边形的每个顶点都有一个法线向量
  * 在vertex阶段处理光照数据
  * 一个顶点一次

<img width="114" alt="image" src="https://user-images.githubusercontent.com/74708198/223992804-4e046e7f-04d2-495b-90e5-65ee35144306.png">

* Phong
  * 每个片元(fragment)或者说每个像素点计算一次光照
  * 像素点的法线向量是通过顶点的法线向量插值得到的
  * 在fragment阶段处理光照数据
  * 一个像素一次开销

## Flat Shading
![image](https://user-images.githubusercontent.com/74708198/224010607-a2856922-afa4-4ba2-b8e9-9e3a051bea20.png)
* 使用Hard Edge模型，每个三角面的顶点法线是同一个方向
```HLSL
Shader "MyCustom/Chapter8_Flat_HardEdge"
{
    Properties
    {
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" }
        LOD 100

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            // make fog work
            #pragma multi_compile_fog

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex           : POSITION;
                float4 normal           : NORMAL;
            };

            struct v2f
            {
                float4 vertex           : SV_POSITION;
                float3 color            : TEXCOORD0;
            };

            uniform float4 _LightColor0;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);

                float3 worldNormal = normalize(mul(unity_ObjectToWorld, v.normal)).xyz;
                float3 lightDir = normalize(_WorldSpaceLightPos0.xyz);
                // Half Lambert
                o.color = saturate(dot(worldNormal, lightDir) * 0.5 + 0.5) * _LightColor0.rgb;
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                return float4(i.color, 1);
            }
            ENDCG
        }
    }
}
```
* 使用Soft Edge模型，相邻三角面的顶点法线是同一个方向
  * 编写 Geometry Shader求得每个三角面的面法线，并赋予顶点法线
    * Shader执行顺序
      <br>compute shader -> vertex shader -> tessellation -> geometry shader -> pixel shader
    * 不要超过[maxvertexcount[n]], Append顶点组成三角形的时候注意顺序
  * 使用偏导数 ddx 和 ddy 计算面法线
    * HLSL：ddx(x), ddy(y); GLSL: dFdx(x), dFdy(y)
    * 偏导函数都只能在fragment shader阶段处理
    * 在VertexShader中，将顶点的Pos传入到FragmentShader中
    * 在到FragmentShader中，调用ddx(Pos)和ddy(Pos)求出相邻的2个像素块之间坐标的差值
<br><img width="415" alt="image" src="https://user-images.githubusercontent.com/74708198/224009880-113c9719-04d6-4a5a-af8f-871657031738.png">  <img width="400" alt="image" src="https://user-images.githubusercontent.com/74708198/224009907-df1e7c53-f09e-4825-8065-ade98b95c8f4.png">

```HLSL
Shader "MyCustom/Chapter8_Flat_SoftEdge_Geom"
{
    Properties
    {
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" }
        LOD 100

        Pass
        {
            CGPROGRAM
            // #pragma + 调用的shader + 命名
            #pragma vertex vert 
            #pragma geometry geom
            #pragma fragment frag
            // make fog work
            #pragma multi_compile_fog

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex           : POSITION;
                float4 normal           : NORMAL;
            };

            struct v2g  // 从顶点着色器-->>几何着色器
            {
                float4 vertex           : SV_POSITION;
                float4 pos              : TEXCOORD0;
            };

            struct g2f  // 从几何着色器-->>片元着色器
            {
                float4 vertex           : SV_POSITION;
                float3 color            : TEXCOORD0;
            };

            uniform float4 _LightColor0;

            v2g vert (appdata v)
            {
                v2g o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.pos = v.vertex;                           // 保存一下模型顶点信息
                return o;
            }

            // 定义：在进行 geomtry shader 顶点处理时不能超过某一个值
            [maxvertexcount(3)]
            void geom(triangle v2g IN[3], inout TriangleStream<g2f> triStream) // 结构固定>>>> ...(Triangle v2g 输入三个顶点，inout TriangleStream<g2f> 输出到一个面，triStream)
            {
                g2f o; //初始化输出结构

                // 计算 面法线
                float3 vecA = IN[1].pos - IN[0].pos;
                float3 vecB = IN[2].pos - IN[0].pos;

                // 叉乘得到法线
                float3 normal = cross(vecA, vecB);
                // 转换至世界空间法线
                float3 worldNormal = normalize(mul((float3x3)unity_ObjectToWorld, normal));

                float3 lightDir = normalize(_WorldSpaceLightPos0.xyz);
                o.color = saturate(dot(worldNormal, lightDir) * 0.5 + 0.5) * _LightColor0.rgb;

                // 告诉几何着色器 三角面由哪几个顶点构成的
                for (int i = 0; i < 3; i++)
                {
                    o.vertex = IN[i].vertex;
                    triStream.Append(o); //Append 合并
                }
            }

            fixed4 frag (g2f i) : SV_Target
            {
                return float4(i.color, 1);
            }
            ENDCG
        }
    }
}
```

```HLSL
Shader "MyCustom/Chapter8_Flat_SoftEdge_DDXY"
{
    Properties
    {
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" }
        LOD 100

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            // make fog work
            #pragma multi_compile_fog

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex           : POSITION;
            };

            struct v2f
            {
                float4 vertex           : SV_POSITION;
                float3 worldPos         : TEXCOORD0;
            };

            uniform float4 _LightColor0;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                //转换为世界坐标的顶点
                o.worldPos = mul(unity_ObjectToWorld, v.vertex);
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                float3 lightDir = normalize(_WorldSpaceLightPos0.xyz);

                // 偏导数计算
                float3 dpdx = ddx(i.worldPos);
                float3 dpdy = ddy(i.worldPos);

                // 求出法线
                float3 worldNormal = normalize(cross(dpdy, dpdx));
                float3 color = saturate(dot(worldNormal, lightDir) * 0.5 + 0.5) * _LightColor0.rgb;

                return float4(color, 1);
            }
            ENDCG
        }
    }
}
```

## Gouraud Shading (Untiy多种光源照射自适应初步处理方式讲解)
![image](https://user-images.githubusercontent.com/74708198/224015570-d2ef5251-a306-4dd9-ae12-62a50f3841c1.png)

```HLSL
Shader "MyCustom/Chapter8_Gouraud"
{
    Properties
    {
        _SpecularColor          ("Specular Color",  color) = (1.0, 1.0, 1.0, 1.0)
        _Shininess              ("Shininess",       float) = 20.0
        _Power                  ("Power",           float) = 10.0       
    }
    SubShader
    {
        Tags { "RenderType"="Opaque"}
        LOD 100

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            // make fog work
            #pragma multi_compile_fog

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex       : POSITION;
                float4 normal       : NORMAL;
            };

            struct v2f
            {
                float4 vertex : SV_POSITION;
                float3 color : TEXCOORD0;
            };

            uniform float4 _LightColor0;
            float4 _SpecularColor;
            float _Shininess;
            float _Power;

            float3 blinnPhongSpecular(float3 normal, float3 viewDir, float3 lightDir)
            {
                float3 halfAngle = normalize(viewDir + lightDir);
                float3 specular = _LightColor0.rgb * _SpecularColor.rgb * pow( saturate(dot(halfAngle, normal)), _Shininess);
                return specular;
            }

            float3 phongSpecular(float3 normal, float3 viewDir, float3 lightDir)
            {
                float3 ref = normalize(reflect(-lightDir, normal));
                float3 specular = _LightColor0.rgb * _SpecularColor.rgb * pow( saturate(dot(ref, viewDir)), _Shininess) * _Power;
                return specular;
            }

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);

                float3 worldNormal = normalize(mul((float3x3)unity_ObjectToWorld, v.normal));
                float3 worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

                float3 lightDir = normalize(_WorldSpaceLightPos0.xyz - worldPos * _WorldSpaceLightPos0.w);
                float3 viewDir = normalize(_WorldSpaceCameraPos.xyz - worldPos);

                float3 specular = phongSpecular(worldNormal, viewDir, lightDir);
                o.color = saturate(dot(worldNormal, lightDir) * 0.5 + 0.5) * _LightColor0.rgb + specular;

                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                float3 color = i.color;
                return float4(color, 1);    
            }
            ENDCG
        }
    }
}
```
## Phong Shading
![image](https://user-images.githubusercontent.com/74708198/224015601-ea4deaea-dbff-4fbc-8f03-0b1752f0c777.png)

```HLSL
Shader "MyCustom/Chapter8_Phong"
{
    Properties
    {
        _SpecularColor          ("Specular Color",  color) = (1.0, 1.0, 1.0, 1.0)
        _Shininess              ("Shininess",       float) = 20.0
        _Power                  ("Power",           float) = 10.0       
    }
    SubShader
    {
        Tags { "RenderType"="Opaque"}
        LOD 100

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            // make fog work
            #pragma multi_compile_fog

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex       : POSITION;
                float4 normal       : NORMAL;
            };

            struct v2f
            {
                float4 vertex       : SV_POSITION;
                float3 worldNormal  : TEXCOORD0;
                float3 viewDir      : TEXCOORD1;
            };

            uniform float4 _LightColor0;
            float4 _SpecularColor;
            float _Shininess;
            float _Power;

            float3 blinnPhongSpecular(float3 normal, float3 viewDir, float3 lightDir)
            {
                float3 halfAngle = normalize(viewDir + lightDir);
                float3 specular = _LightColor0.rgb * _SpecularColor.rgb * pow( saturate(dot(halfAngle, normal)), _Shininess);
                return specular;
            }

            float3 phongSpecular(float3 normal, float3 viewDir, float3 lightDir)
            {
                float3 ref = normalize(reflect(-lightDir, normal));
                float3 specular = _LightColor0.rgb * _SpecularColor.rgb * pow( saturate(dot(ref, viewDir)), _Shininess) * _Power;
                return specular;
            }

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);

                o.worldNormal = normalize(mul((float3x3)unity_ObjectToWorld, v.normal));
                
                float3 worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                o.viewDir = normalize(_WorldSpaceCameraPos.xyz - worldPos);

                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                float3 worldNormal = i.worldNormal;

                float3 lightDir = normalize(_WorldSpaceLightPos0.xyz);
                float3 viewDir = normalize(i.viewDir);

                float3 specular = phongSpecular(worldNormal, viewDir, lightDir);
                float3 color = saturate(dot(worldNormal, lightDir) * 0.5 + 0.5) * _LightColor0.rgb + specular;

                return float4(color, 1);    
            }
            ENDCG
        }
    }
}
```


