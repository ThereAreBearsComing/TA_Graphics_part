# 2.4 Traditional Light Model
## 传统光照模型
![image](https://user-images.githubusercontent.com/74708198/187167737-00fda841-63d4-4934-8e14-9c712d400e55.png)
## 1. Light source
### 1.1 Diffuse 
### 1.1.1 Lambert 
![image](https://user-images.githubusercontent.com/74708198/187170320-711db760-ff3d-49e5-a94d-9073da86245b.png)
![image](https://user-images.githubusercontent.com/74708198/187170639-25bc88f6-155e-4a9c-87d2-d9ef85f4d904.png)

* 补充：
  * 首先其中对应GAMES101中的辐射度量学中的一个概念
    * Irradiance翻译为辐照度
  * Lambert余弦定律结论：n（法线方向）和 l（光照方向）的夹角决定了明暗

* Lambert光照模型
* ![image](https://user-images.githubusercontent.com/74708198/187218029-bce28cff-c737-40af-aeb4-b5ef50d7cb1f.png)
* ShaderForge:
* ![image](https://user-images.githubusercontent.com/74708198/187218323-6e8e5c54-e6fd-496a-98e2-4ddb4947f019.png)
* ShaderCode:
```HLSL
Shader "LearnZDTA/L3/Lambert" {
    Properties {
    }
    SubShader {
        Tags {
            "RenderType"="Opaque"
        }
        Pass {
            Name "FORWARD"
            Tags {
                "LightMode"="ForwardBase"
            }


            CGPROGRAM
            #pragma vertex vert
            #pr*agma fragment frag
            #include "UnityCG.cginc"
            #pragma multi_compile_fwdbase_fullshadows
            #pragma target 3.0
            // 输入结构
            struct VertexInput {
                float4 vertex : POSITION;   // 将模型顶点信息输入进来
                float4 normal : NORMAL;     // 将模型法线信息输入进来
            };
            // 输出结构
            struct VertexOutput {
                float4 pos : SV_POSITION;   // 由模型顶点信息换算而来的顶点屏幕位置
                float3 nDirWS : TEXCOORD0;  // 由模型法线信息换算来的世界空间法线信息
            };
            // 输入结构>>>顶点Shader>>>输出结构
            VertexOutput vert (VertexInput v) {
                VertexOutput o = (VertexOutput)0;               // 新建一个输出结构
                o.pos = UnityObjectToClipPos( v.vertex );       // 变换顶点信息 并将其塞给输出结构
                o.nDirWS = UnityObjectToWorldNormal(v.normal);  // 变换法线信息 并将其塞给输出结构
                return o;                                       // 将输出结构 输出
            }
            // 输出结构>>>像素
            float4 frag(VertexOutput i) : COLOR {
                float3 nDir = i.nDirWS;                         // 获取nDir
                float3 lDir = _WorldSpaceLightPos0.xyz;         // 获取lDir
                float nDotl = dot(i.nDirWS, lDir);              // nDir点积lDir
                float lambert = max(0.0, nDotl);                // 截断负值
                return float4(lambert, lambert, lambert, 1.0);  // 输出最终颜色
            }
            ENDCG
        }
    }
    FallBack "Diffuse"
}
```

### 1.1.2 Half-Lambert
* Lambert看起来不错，但暗部死黑：
* ![image](https://user-images.githubusercontent.com/74708198/187218610-e89cbfd3-6c2d-4510-a389-b837543a57e8.png)
* ShaderForge:
* ![image](https://user-images.githubusercontent.com/74708198/187218742-da8a49fc-7ad6-413a-b6ba-23b44df93d9b.png)
* ShaderCode:
```HLSL
Shader "AP01/L04/HalfLambert" {
    Properties {
    }
    SubShader {
        Tags {
            "RenderType"="Opaque"
        }
        Pass {
            Name "FORWARD"
            Tags {
                "LightMode"="ForwardBase"
            }


            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            #pragma multi_compile_fwdbase_fullshadows
            #pragma target 3.0
            // 输入结构
            struct VertexInput {
                float4 vertex : POSITION;   // 将模型顶点信息输入进来
                float4 normal : NORMAL;     // 将模型法线信息输入进来
            };
            // 输出结构
            struct VertexOutput {
                float4 pos : SV_POSITION;   // 由模型顶点信息换算而来的顶点屏幕位置
                float3 nDirWS : TEXCOORD0;  // 由模型法线信息换算来的世界空间法线信息
            };
            // 输入结构>>>顶点Shader>>>输出结构
            VertexOutput vert (VertexInput v) {
                VertexOutput o = (VertexOutput)0;               // 新建一个输出结构
                o.pos = UnityObjectToClipPos( v.vertex );       // 变换顶点信息 并将其塞给输出结构
                o.nDirWS = UnityObjectToWorldNormal(v.normal);  // 变换法线信息 并将其塞给输出结构
                return o;                                       // 将输出结构 输出
            }
            // 输出结构>>>像素
            float4 frag(VertexOutput i) : COLOR {
                float3 nDir = i.nDirWS;                         // 获取nDir
                float3 lDir = _WorldSpaceLightPos0.xyz;         // 获取lDir
                float nDotl = dot(i.nDirWS, lDir);              // nDir点积lDir
                float halfLambert = nDotl * 0.5 + 0.5;          // 映射至0~1
                return float4(halfLambert, halfLambert, halfLambert, 1.0);  // 输出最终颜色
            }
            ENDCG
        }
    }
    FallBack "Diffuse"
}
```

* 补充：调子映射
* ![image](https://user-images.githubusercontent.com/74708198/187219111-828a41ee-d8be-4433-813d-1ebeef298425.png)
* ![image](https://user-images.githubusercontent.com/74708198/187218960-3ef13777-917d-4d02-aa46-268bf68b4ffa.png)

### 1.1.2 Overall
* Lambert和HalfLambert的区别：
  * Lambert把点乘后的结果暴力截断到了（0,1）
  * HalfLamber把点乘后的结果映射到了（0,1）
* “结构”“函数/方法”等基础概念，参考C#
* 渲染流程参考渲染流水线
* 调子映射：
  * 将一张贴图的uv映射到光照的（0,1）上

### 1.2 Specular
### 1.2.1 Phong
![image](https://user-images.githubusercontent.com/74708198/187170713-fa98763a-bfe7-4504-a26a-0dede951d6d3.png)
* 用环境光照来代替间接光照
* 将几项加起来，形成一个相对比较完整的光照模型
* Phong模型的算法：
  * Phong = Pow(dot(v，r),_gloss)

* ShaderForge:
* ![image](https://user-images.githubusercontent.com/74708198/187222361-3514bf2a-ca38-4efc-98f7-134a58e87e00.png)

* ShaderCode(OldSchool_plus = Lambert + Phong + 3Col-Ambient):
```HLSL
Shader "LearnZDTA/L7/OldSchool_Plus"
{
    Properties
    {
        _SpecularPower("SpecularPower",range(1,99)) = 30	//面板需要的参数
        _EnvIntensity("EnvIntensity",range(0,1)) = 0.3
        _BaseColor("BaseColor",color) = (1.0,1.0,1.0,1.0)
        _LightColor("LightColor",color) = (1.0,1.0,1.0,1.0)
        _TopColor("TopColor",color) = (1.0,1.0,1.0,1.0)
        _BottomColor("BottomColor",color) = (1.0,1.0,1.0,1.0)
        _SideColor("SideColor",color) = (1.0,1.0,1.0,1.0)
        _Occlusion("AO",2d) ="white"{} 
        
    }
    SubShader
    {
        Name "FORWARD"
        Tags { "LightMode"="ForwardBase" }
        
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            #include "AutoLight.cginc"  //投影相关头文件
            #include "Lighting.cginc"

            #pragma multi_compile_fwdbase_fullshadows
            #pragma target 3.0

            float _SpecularPower;	//声明变量
            float _EnvIntensity;
            sampler2D _Occlusion;
            float3 _BaseColor;
            float3 _LightColor;
            float3 _TopColor;
            float3 _BottomColor;
            float3 _SideColor;
            

            struct vertexInput
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float2 uv0 : TEXCOORD0;
                
            };

            struct vertexOutput
            {
                float4 pos :SV_POSITION;    //注意！此处必须命名为pos，需要和Unity封装好的方法的参数对应
                float4 posWS : TEXCOORD0;
                float3 nDirWS : TEXCOORD1;
                float2 uv : TEXCOORD2;
                LIGHTING_COORDS(3,4)    //投影相关
            };

            vertexOutput vert (vertexInput v)
            {
                vertexOutput o = (vertexOutput)0;;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.posWS = mul(unity_ObjectToWorld,v.vertex);
                o.nDirWS = UnityObjectToWorldNormal(v.normal);
                o.uv = v.uv0;
                TRANSFER_VERTEX_TO_FRAGMENT(o)  //投影相关
                return o;
            }

            fixed4 frag (vertexOutput i) : SV_Target
            {
                //准备
                float3 nDir = normalize(i.nDirWS);
                float3 lDir = _WorldSpaceLightPos0.xyz;
                float3 vDir = normalize(_WorldSpaceCameraPos.xyz - i.posWS.xyz);
                float3 rDir = reflect(-lDir,nDir);
                
                //计算
                float nDotl = dot(nDir,lDir);
                float vDotr = dot(vDir,rDir);

                //直接光照
                float Lambert = max(0.0,nDotl);
                float Phong = pow(max(0.0,vDotr),_SpecularPower);
                float shadow = LIGHT_ATTENUATION(i);
                float3 DirectLight = (_BaseColor * Lambert + Phong) * _LightColor * shadow;


                //环境光
                float topmask = max(0.0,nDir.g);
                float bottommask = max(0.0,-nDir.g);
                float sidemask = 1.0 - topmask - bottommask;
                float totalmask = _TopColor*topmask + _BottomColor*bottommask + _SideColor*sidemask;
                float3 envColor = mul(_BaseColor,mul(_EnvIntensity,totalmask));
                float occlusion = tex2D(_Occlusion,i.uv);
                float3 EnvLight = envColor * occlusion;

                float3 FinalRGB =DirectLight + EnvLight;
                return float4(FinalRGB,1.0);
            }
            ENDCG
        }
    }
    FallBack  "Diffuse"
}
```

### 1.2.2 Blinn-Phong
* ![image](https://user-images.githubusercontent.com/74708198/187221555-1a1deb3a-c362-48d3-af74-2b417b118341.png)

* ShaderForge:
* ![image](https://user-images.githubusercontent.com/74708198/187222162-7272bb31-55e2-476d-bc9f-5dae1a97a7f9.png)

* ShaderCode(OldSchool = Lambert + Blinn-Phong):
```HLSL
Shader "AP01/L05/OldSchool" {
    Properties {
        _MainCol ("颜色", color) = (1.0, 1.0, 1.0, 1.0)
        _SpecularPow ("高光次幂", range(1, 90)) = 30
    }
    SubShader {
        Tags {
            "RenderType"="Opaque"
        }
        Pass {
            Name "FORWARD"
            Tags {
                "LightMode"="ForwardBase"
            }


            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            #pragma multi_compile_fwdbase_fullshadows
            #pragma target 3.0
            // 输入参数
            // 修饰字（满足小朋友太多的问好, 想保发量的大家看热闹）
                // uniform  共享于vert,frag
                // attibute 仅用于vert
                // varying  用于vert,frag传数据
            uniform float3 _MainCol;     // RGB够了 float3
            uniform float _SpecularPow;  // 标量 float
            // 输入结构
            struct VertexInput {
                float4 vertex : POSITION;   // 顶点信息 Get✔
                float4 normal : NORMAL;     // 法线信息 Get✔
            };
            // 输出结构
            struct VertexOutput {
                float4 posCS : SV_POSITION;     // 裁剪空间（暂理解为屏幕空间吧）顶点位置
                float4 posWS : TEXCOORD0;       // 世界空间顶点位置
                float3 nDirWS : TEXCOORD1;      // 世界空间法线方向
            };
            // 输入结构>>>顶点Shader>>>输出结构
            VertexOutput vert (VertexInput v) {
                VertexOutput o = (VertexOutput)0;                   // 新建输出结构
                    o.posCS = UnityObjectToClipPos( v.vertex );     // 变换顶点位置 OS>CS
                    o.posWS = mul(unity_ObjectToWorld, v.vertex);   // 变换顶点位置 OS>WS
                    o.nDirWS = UnityObjectToWorldNormal(v.normal);  // 变换法线方向 OS>WS
                return o;                                           // 返回输出结构
            }
            // 输出结构>>>像素
            float4 frag(VertexOutput i) : COLOR {
                // 准备向量
                float3 nDir = normalize(i.nDirWS);
                float3 lDir = _WorldSpaceLightPos0.xyz;
                float3 vDir = normalize(_WorldSpaceCameraPos.xyz - i.posWS.xyz);
                float3 hDir = normalize(vDir + lDir);
                // 准备点积结果
                float ndotl = dot(nDir, lDir);
                float ndoth = dot(nDir, hDir);
                // 光照模型
                float lambert = max(0.0, ndotl);
                float blinnPhong = pow(max(0.0, ndoth), _SpecularPow);
                float3 finalRGB = _MainCol * lambert + blinnPhong;
                // 返回结果
                return float4(finalRGB, 1.0);
            }
            ENDCG
        }
    }
    FallBack "Diffuse"
}
```
## 2. Environment Light
### 2.1 Diffuse
### 2.1.1 Singal-Col-Ambient
* **核心思路：**就是采样贴图（环境遮蔽图-DCC软件烘培得），乘以一个光的颜色。
* ![image](https://user-images.githubusercontent.com/74708198/187223783-9ae3d342-107b-4b4e-8bfb-9e6bc378dee4.png)

### 2.1.2 3Col-Ambient（通过法线颜色来分别发现朝向）
* **核心思路：**将环境光分为三个部分：顶光、底光、其余环境光
* **实现：**
  * 前提知识：法线n的G通道是顶光
     * ![image](https://user-images.githubusercontent.com/74708198/187224537-7989dc08-4fef-423f-8315-2721b46b86a8.png)
  * 那么法线G通道取反，就是底光
     * ![image](https://user-images.githubusercontent.com/74708198/187224596-6911d048-51b6-4471-b9e5-28c0c65e48fe.png)
* 有了上述的前提知识，我们就可以分离出三个部分的环境光，各自单独处理
* **顶光部分**
    * 法线取G通道，max截掉负数部分，乘以顶部光颜色即可
* **底光部分**
    * 法线取反（乘-1），max截掉负数部分，乘以底部光颜色即可
* **其余环境光**
    * 有了顶光和底光之后，用1减去顶光和底光，就是剩余的环境光、
    * //注意，用1减的应该是顶光和底光未乘以颜色的值，而不是乘了颜色之后的值
* ShaderForge:
* ![image](https://user-images.githubusercontent.com/74708198/187225427-9538f56e-a2eb-453d-aa2a-eac5d77c6270.png)
* ShaderCode:
  * 第一步肯定找模板复制，改shader路径名，略
  * 思考以下问题：
    * 1.需要哪些参数？
    * 2.输入结构需要哪些数据？
    * 3.因为输入结构的变动，后续的vertex shader，输出结构需要做哪些变动?
    * 4.fragment shader怎么写？
  * 答案:
    * 1.需要以下参数
      * 三个Color颜色参数（分别代表顶、底、剩余的环境光颜色）
      * tex2d（采样occlusion图）
    * 2.需要以下信息
      * 顶点信息
      * 法线
      * uv（采样occlusion图）
    * 3、4略，看具体代码
```HLSL
Shader "LearnZDTA/L7/3ColorAmbient"
{
    Properties
    {
        _Occlusion("Occlusion",2d) = "white"{}
        _TopCol("TopColor",color) = (1.0,1.0,1.0,1.0)
        _BottomCol("BottomColor",color) = (0.5,0.5,0.5,1.0)
        _SideCol("SideColor",color) = (0.0,0.0,0.0,1.0)
        
    }
    SubShader
    {
        Tags {
            "RenderType"="Opaque"
        }
        Pass
        {
            Name "FORWARD"
            Tags {
                "LightMode"="ForwardBase"
            }
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            #pragma multi_compile_fwdbase_fullshadows
            #pragma target 3.0

            struct vertexInput
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float2 uv0 : TEXCOORD0;
            };

            struct vertexOutput
            {
                float4 posCS :SV_POSITION;
                float3 nDirWS : TEXCOORD0;
                float2 uv : TEXCOORD1;
            };

            sampler2D _Occlusion;
            float3 _TopCol;
            float3 _BottomCol;
            float3 _SideCol;

            vertexOutput vert (vertexInput v)
            {
                vertexOutput o = (vertexOutput)0;
                o.posCS = UnityObjectToClipPos(v.vertex);
                o.nDirWS = UnityObjectToWorldNormal(v.normal);
                o.uv = v.uv0;
                return o;
            }

            fixed4 frag (vertexOutput i) : SV_Target
            {
                //准备
                float3 nDir = i.nDirWS;

                //计算遮罩
                float topmask = max(0.0,nDir.g);
                float bottommask = max(0.0,-nDir.g);
                float sidemask = 1.0 - topmask - bottommask;

                //混色
                float3 envColor = _TopCol*topmask + _BottomCol*bottommask + _SideCol*sidemask;

                //采样occlusion
                float occlusion = tex2D(_Occlusion,i.uv);

                //Final
                float3 Final = occlusion * envColor;

                return float4(Final,1.0);
            }
            ENDCG
        }
    }
}
```

### 2.2 Specular
### 2.2.1 Matcap
#### 2.2.1.1 什么是matcap
* 全称：MaterialCapture（材质捕获）
* 一种无视BRDF，将BRDF渲染结果，用View空间法线朝向，直接映射到模型表面的算法
* 常用来模拟环境的镜面反射
#### 2.2.1.2 优缺点
* 优点
  * 很省，效果还ok
* 缺点
  * 只能是静止状态，在一些情况下容易穿帮（视角一动起来就穿帮了）
#### 2.2.1.3 连连看实现
* **核心思路：**
  * 空间转换：因为要用view空间的法线，所以先进行空间转换（SF中有Transform节点可以直接转换，不用像上节切线->世界空间那样写code节点代码）
  * 取view空间法线的RG通道Remap到（0,1）,作为uv对MapCap采样
  * 叠加菲涅尔效果（模拟金属/非金属的不同感）
* **理性认识：**
  * R通道的意义：法线平行于屏幕的朝向渐变（如果将这个（-1,1）的结果Remap到（0,1），就是：法线朝左=0，朝右=1）
* ![image](https://user-images.githubusercontent.com/74708198/187229517-af9c72ba-93d4-4750-8dce-37364c87d764.png)
  * G通道的意义：法线垂直于屏幕朝向的渐变（如果将这个（-1,1）的结果Remap到（0,1），就是：左=0，右=1）
    * 同理，朝下=0，朝上=1
* **关于matcap为什么是圆的：**
* ![image](https://user-images.githubusercontent.com/74708198/187229599-12756031-cc7c-46fa-97df-d786a141f345.png)
  * 取到的view空间的向量是经过normalize（归一化）的，也就是说R通道的平方+G通道的平方 = 1，这就是个圆
* ShaderForge:
* ![image](https://user-images.githubusercontent.com/74708198/187229667-29710187-16d2-491b-b5a8-5a399a30bbd1.png)
* ShaderCode:
```HLSL
Shader "AP01/L09/Matcap" {
    Properties {
        _NormalMap  ("法线贴图", 2D) = "bump" {}
        _Matcap     ("Matcap", 2D) = "gray" {}
        _FresnelPow ("菲涅尔次幂", Range(0, 10)) = 1
        _EnvSpecInt ("环境镜面反射强度", Range(0, 5)) = 1
    }
    SubShader {
        Tags {
            "RenderType"="Opaque"
        }
        Pass {
            Name "FORWARD"
            Tags {
                "LightMode"="ForwardBase"
            }
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            #pragma multi_compile_fwdbase_fullshadows
            #pragma target 3.0
            // 输入参数
            uniform sampler2D _NormalMap;
            uniform sampler2D _Matcap;
            uniform float _FresnelPow;
            uniform float _EnvSpecInt;
            // 输入结构
            struct VertexInput {
                float4 vertex   : POSITION;     // 顶点信息
                float2 uv0      : TEXCOORD0;    // uv信息
                float3 normal   : NORMAL;       // 法线信息
                float4 tangent  : TANGENT;      // 切线信息
            };
            // 输出结构
            struct VertexOutput {
                float4 pos : SV_POSITION;       // 屏幕顶点位置
                float2 uv0 : TEXCOORD0;         // uv信息
                float4 posWS : TEXCOORD1;       // 世界顶点位置
                float3 nDirWS : TEXCOORD2;      // 世界法线方向
                float3 tDirWS : TEXCOORD3;      // 世界切线方向
                float3 bDirWS : TEXCOORD4;      // 世界副切线方向
            };
            // 输入结构>>>顶点Shader>>>输出结构
            VertexOutput vert (VertexInput v) {
                VertexOutput o = (VertexOutput)0;           // 新建一个输出结构
                    o.pos = UnityObjectToClipPos( v.vertex );
                    o.uv0 = v.uv0;                                  // 传递uv信息
                    o.posWS = mul(unity_ObjectToWorld, v.vertex);   // 顶点位置 OS>WS
                    o.nDirWS = UnityObjectToWorldNormal(v.normal);  // 法线方向 OS>WS
                    o.tDirWS = normalize(mul(unity_ObjectToWorld, float4(v.tangent.xyz, 0.0)).xyz); // 切线方向 OS>WS
                    o.bDirWS = normalize(cross(o.nDirWS, o.tDirWS) * v.tangent.w);  // 根据nDir tDir求bDir
                return o;                                   // 将输出结构 输出
            }
            // 输出结构>>>像素
            float4 frag(VertexOutput i) : COLOR {
                // 准备向量
                float3 nDirTS = UnpackNormal(tex2D(_NormalMap, i.uv0)).rgb;
                float3x3 TBN = float3x3(i.tDirWS, i.bDirWS, i.nDirWS);
                float3 nDirWS = normalize(mul(nDirTS, TBN));        // 计算nDirVS 计算Fresnel
                float3 nDirVS = mul(UNITY_MATRIX_V, nDirWS);        // 计算MatcapUV
                float3 vDirWS = normalize(_WorldSpaceCameraPos.xyz - i.posWS.xyz); // 计算Fresnel

                // 准备中间变量
                float vdotn = dot(vDirWS, nDirWS);
                float2 matcapUV = nDirVS.rg * 0.5 + 0.5;

                // 光照模型
                float3 matcap = tex2D(_Matcap, matcapUV);
                float fresnel = pow(max(0.0, 1.0 - vdotn), _FresnelPow);
                float3 envSpecLighting = matcap * fresnel * _EnvSpecInt;

                // 返回值
                return float4(envSpecLighting, 1.0);
            }
            ENDCG
        }
    }
    FallBack "Diffuse"
}
```
### 2.2.2 Cubemap
#### 2.2.2.1 什么是cubemap
* 翻译为“立方体贴图”
* 是环境映射（Environment Mapping）的一种实现方式
* 可以解决环境光记录在球上的扭曲问题
* 将环境映射到6张图上（现在常用的可以是一张）
  * 例如：![image](https://user-images.githubusercontent.com/74708198/187230959-26d28e46-687b-43d5-ba8a-b6cb200f504f.png)
#### 2.2.2.1 连连看实现
* 核心思路：
  * cubemap是将环境信息映射到图上，信息是：采样点上的信息。
  * 如何取出这个信息？-> -v方向经过n反射的方向去采样（vrDir）
    * 理解：不是直接看（v），而是看的反方向经过n反射的方向去采样（reflect(-v，n)）
* ShaderForge:
* ![image](https://user-images.githubusercontent.com/74708198/187231198-0461239c-b30c-4571-9795-49a182045abd.png)
* ShaderCode:
  * 以matcap为模板进行修改
  * 注意
    * 代表cubemap的参数的类型已经是为cubemap专门准备的 samplerCUBE
    * 输入输出部分无需修改（所需数据一样）
    * fragment shader：
      * 准备工作：
        * 需要计算vrDir（采样cubemap的方向）
        * 不需要view空间的法线了（不做matcap）
      * 计算：
        * 不需要matcap相关的计算
      * 光照计算：
        * 计算cubemap（texCUBElod中参数（cubemap，float4类型），其中float4类型中xyz代表vrDIR,w分量代表LOD级别）
        * 菲涅尔不用改
    * //因为是环境光部分，所以肯定是要受到AO的影响的，完整的情况应该加上AO（看后续的OldSchoolPro）
 ```HLSL
 Shader "AP01/L09/Cubemap" {
    Properties {
        _Cubemap    ("环境球", Cube) = "_Skybox" {}
        _NormalMap  ("法线贴图", 2D) = "bump" {}
        _CubemapMip ("环境球Mip", Range(0, 7)) = 0
        _FresnelPow ("菲涅尔次幂", Range(0, 5)) = 1
        _EnvSpecInt ("环境镜面反射强度", Range(0, 5)) = 0.2
    }
    SubShader {
        Tags {
            "RenderType"="Opaque"
        }
        Pass {
            Name "FORWARD"
            Tags {
                "LightMode"="ForwardBase"
            }
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            #pragma multi_compile_fwdbase_fullshadows
            #pragma target 3.0
            // 输入参数
            uniform samplerCUBE _Cubemap;
            uniform sampler2D _NormalMap;
            uniform float _CubemapMip;
            uniform float _FresnelPow;
            uniform float _EnvSpecInt;
            // 输入结构
            struct VertexInput {
                float4 vertex   : POSITION;     // 顶点信息
                float2 uv0      : TEXCOORD0;    // uv信息
                float3 normal   : NORMAL;       // 法线信息
                float4 tangent  : TANGENT;      // 切线信息
            };
            // 输出结构
            struct VertexOutput {
                float4 pos : SV_POSITION;       // 屏幕顶点位置
                float2 uv0 : TEXCOORD0;         // uv信息
                float4 posWS : TEXCOORD1;       // 世界顶点位置
                float3 nDirWS : TEXCOORD2;      // 世界法线方向
                float3 tDirWS : TEXCOORD3;      // 世界切线方向
                float3 bDirWS : TEXCOORD4;      // 世界副切线方向
            };
            // 输入结构>>>顶点Shader>>>输出结构
            VertexOutput vert (VertexInput v) {
                VertexOutput o = (VertexOutput)0;           // 新建一个输出结构
                    o.pos = UnityObjectToClipPos( v.vertex );
                    o.uv0 = v.uv0;                                  // 传递uv信息
                    o.posWS = mul(unity_ObjectToWorld, v.vertex);   // 顶点位置 OS>WS
                    o.nDirWS = UnityObjectToWorldNormal(v.normal);  // 法线方向 OS>WS
                    o.tDirWS = normalize(mul(unity_ObjectToWorld, float4(v.tangent.xyz, 0.0)).xyz); // 切线方向 OS>WS
                    o.bDirWS = normalize(cross(o.nDirWS, o.tDirWS) * v.tangent.w);  // 根据nDir tDir求bDir
                return o;                                   // 将输出结构 输出
            }
            // 输出结构>>>像素
            float4 frag(VertexOutput i) : COLOR {
                // 准备向量
                float3 nDirTS = UnpackNormal(tex2D(_NormalMap, i.uv0)).rgb;
                float3x3 TBN = float3x3(i.tDirWS, i.bDirWS, i.nDirWS);
                float3 nDirWS = normalize(mul(nDirTS, TBN));   // 计算Fresnel 计算vrDirWS
                float3 vDirWS = normalize(_WorldSpaceCameraPos.xyz - i.posWS.xyz);  // 计算Fresnel
                float3 vrDirWS = reflect(-vDirWS, nDirWS);// 采样Cubemap

                // 准备中间变量
                float vdotn = dot(vDirWS, nDirWS);

                // 光照模型
                float3 var_Cubemap = texCUBElod(_Cubemap, float4(vrDirWS, _CubemapMip)).rgb;
                float fresnel = pow(max(0.0, 1.0 - vdotn), _FresnelPow);
                float3 envSpecLighting = var_Cubemap * fresnel * _EnvSpecInt;

                // 返回值
                return float4(envSpecLighting, 1.0);
            }
            ENDCG
        }
    }
    FallBack "Diffuse"
}
```

### 2.2.3 Fresnel
* **菲涅尔：**
  * 在现实世界中，观察非金属表面时，会有下边这种现象：`视线垂直于表面时，反射较弱，而当视线非垂直表面时，夹角越小，反射越明显。`
    * 例1：看水面时近处（视线接近垂直）可以看到水里的场景，看远处时缺只能看到反射的太阳光
    * ![image](https://user-images.githubusercontent.com/74708198/187228098-595a32fe-e13b-4493-bfd9-f4ec160ad0eb.png)
    * 例2：图中的书，平看会反射，垂直看几乎不反射
    * ![image](https://user-images.githubusercontent.com/74708198/187228285-7ba184b3-cb46-4c4a-bb16-7d7e73a3d589.png)
    * 例3：你在坐公交车时，向外看窗户可以看到外边的景色；但是当你看车前排人的窗户是，看到的确实反射出的人脸。
    * 例4：肥皂泡
    * ![image](https://user-images.githubusercontent.com/74708198/187228408-acbdff35-f183-465a-b550-5317f3d7a5f5.png)

* ShaderForge:
* ![image](https://user-images.githubusercontent.com/74708198/187227723-2d935521-16b3-4af7-addf-aada68406199.png)

### 2.3 Shadow
* ShaderForge:
* ![image](https://user-images.githubusercontent.com/74708198/187226312-0a0bb059-9b20-44fc-876a-6ed241c90ed2.png)
* ShaderCode:
```HLSL
Shader "LearnZDTA/L7/Shadow"
{
    Properties
    {
    }
    SubShader
    {
        Tags {
            "RenderType"="Opaque"
        }
        Pass
        {
            Name "FORWARD"
            Tags {
                "LightMode"="ForwardBase"
            }
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            #include "AutoLight.cginc" // 使用Unity投影必须包含这两个库文件
            #include "Lighting.cginc" // 同上
            #pragma multi_compile_fwdbase_fullshadows
            #pragma target 3.0

            struct vertexInput
            {
                float4 vertex : POSITION;
            };

            struct vertexOutput
            {
                float4 pos :SV_POSITION;
                LIGHTING_COORDS(0,1) // 投影用坐标信息 Unity已封装 不用管细节
            };

            vertexOutput vert (vertexInput v)
            {
                vertexOutput o = (vertexOutput)0;
                o.pos = UnityObjectToClipPos(v.vertex);
                TRANSFER_VERTEX_TO_FRAGMENT(o)
                return o;
            }

            fixed4 frag (vertexOutput i) : SV_Target
            {
                float shadow = LIGHT_ATTENUATION(i); // 同样Unity封装好的函数 可取出投影
                return float4(shadow,shadow,shadow,1.0);
            }
            ENDCG
        }
    }
        FallBack "Diffuse"
}
```
## 完整经验光照模型(SchoolPlus = Lambert + Phong + 3ColA + Fresnel + Cubemap + shadow)
* ShaderForge:
* ![image](https://user-images.githubusercontent.com/74708198/187233780-62e9559e-7643-4333-b43e-e789d087fe45.png)
* Code:
```HLSL
Shader "LearnZDTA/L9/OldSchool_Pro"
{
    Properties
    {
        [Header(texture)]
            _MainTex    ("RGB:基础颜色 A:环境遮罩", 2D)     = "white" {}
            _NormTex	("RGB:法线贴图", 2D)               = "bump" {}
            _SpecTex    ("RGB:高光颜色 A:高光次幂", 2D)      = "gray" {}
            _EmitTex    ("RGB:自发光贴图", 2d)                = "black" {}
            _Cubemap    ("RGB:环境贴图", cube)              = "_Skybox" {}
        [Header(Diffuse)]
            _MainCol("基本色",Color)                       = (0.5, 0.5, 0.5, 1.0)
            _EnvDiffInt ("环境漫反射强度",  Range(0, 1))    = 0.2
            _EnvUpCol   ("环境天顶颜色", Color)             = (1.0, 1.0, 1.0, 1.0)
            _EnvSideCol ("环境水平颜色", Color)             = (0.5, 0.5, 0.5, 1.0)
            _EnvDownCol ("环境地表颜色", Color)             = (0.0, 0.0, 0.0, 0.0)
        [Header(Specular)]
            _SpecPow    ("高光次幂",    Range(1, 90))       = 30
            _EnvSpecInt ("环境镜面反射强度", Range(0, 5))   = 0.2
            _FresnelPow ("菲涅尔次幂", Range(0, 5))         = 1
            _CubemapMip ("环境球Mip级别", Range(1, 7))          = 1
        [Header(Emission)]
            _EmitInt("自发光强度", range(1, 10)) = 1
        }
    SubShader
    {
        Tags {
            "RenderType"="Opaque"
        }
        
        Pass
        {
            Name "FORWARD"
            Tags { "LightMode"="ForwardBase" }
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            #include "AutoLight.cginc"  //投影相关头文件
            #include "Lighting.cginc"

            #pragma multi_compile_fwdbase_fullshadows
            #pragma target 3.0
            //##声明面板参数##
            // Texture
            uniform sampler2D _MainTex;
            uniform sampler2D _NormTex;
            uniform sampler2D _SpecTex;
            uniform sampler2D _EmitTex;
            uniform samplerCUBE _Cubemap;
            // Diffuse
            uniform float3 _MainCol;
            uniform float _EnvDiffInt;
            uniform float3 _EnvUpCol;
            uniform float3 _EnvSideCol;
            uniform float3 _EnvDownCol;
            // Specular
            uniform float _SpecPow;
            uniform float _FresnelPow;
            uniform float _EnvSpecInt;
            uniform float _CubemapMip;
            // Emission
            uniform float _EmitInt;
            

            struct vertexInput
            {
                float4 vertex : POSITION;    //顶点信息
                float2 uv0 : TEXCOORD0;     //uv
                float4 normal : NORMAL;  //法线
                float4 tangent : TANGENT; //切线
                
            };

            struct vertexOutput
            {
                float4 pos : SV_POSITION;
                float2 uv0 : TEXCOORD0;
                float4 posWS    : TEXCOORD1;  // WS顶点位置
                float3 nDirWS   : TEXCOORD2;  // WS法线方向
                float3 tDirWS   : TEXCOORD3;  // WS切线方向
                float3 bDirWS   : TEXCOORD4;  // WS副切线方向
                LIGHTING_COORDS(5,6)          // 投影相关
            };

            vertexOutput vert (vertexInput v)
            {
                vertexOutput o = (vertexOutput)0;
                    o.pos = UnityObjectToClipPos( v.vertex ); //CS顶点信息
                    o.uv0 = v.uv0;  //法线
                    o.posWS = mul(unity_ObjectToWorld, v.vertex);    //WS顶点信息
                    o.tDirWS = normalize(mul(unity_ObjectToWorld, float4(v.tangent.xyz, 0.0)).xyz); //WS切线
                    o.nDirWS = UnityObjectToWorldNormal(v.normal);  //WS法线
                    o.bDirWS = normalize(cross(o.nDirWS, o.tDirWS) * v.tangent.w);  //WS副切线
                    //这里注意！因为b的计算用到了t和n，所以在写的时候要写在t和n之后
                    TRANSFER_VERTEX_TO_FRAGMENT(o)                  // 投影相关
                return o;
                
            }

            fixed4 frag (vertexOutput i) : SV_Target
            {
                 //向量准备
                 float3 nDirTS = UnpackNormal(tex2D(_NormTex, i.uv0)).rgb;
                 float3x3 TBN = float3x3(i.tDirWS, i.bDirWS, i.nDirWS);
                 float3 nDirWS = normalize(mul(nDirTS, TBN));
                 float3 vDirWS = normalize(_WorldSpaceCameraPos.xyz - i.posWS.xyz);//v方向=相机位置-所在位置
                 float3 vrDirWS = reflect(-vDirWS, nDirWS);
                 float3 lDirWS = _WorldSpaceLightPos0.xyz;
                 float3 lrDirWS = reflect(-lDirWS, nDirWS);
                
                 //中间计算
                 float ndotl = dot(nDirWS, lDirWS); //供Lambert使用
                 float vdotr = dot(vDirWS, lrDirWS); //Phong使用
                 float vdotn = dot(vDirWS, nDirWS); //Fresnel使用
                
                 //采样
                 float4 var_MainTex = tex2D(_MainTex, i.uv0);
                 float4 var_SpecTex = tex2D(_SpecTex, i.uv0);
                 float3 var_EmitTex = tex2D(_EmitTex, i.uv0).rgb;
                 float cubeMapMip = lerp(_CubemapMip, 0.0, var_SpecTex.a);//SpecTex的a通道理解为光滑度，达到光滑处反射比粗糙处清晰的效果
                 float3 var_Cubemap = texCUBElod(_Cubemap, float4(vrDirWS,cubeMapMip)).rgb;

                //光照计算
                     //DirectDif
                     float3 baseCol = var_MainTex.rgb * _MainCol;
                     float lambert = max(0.0,ndotl);
                     //DirectSpe
                     float specCol = var_SpecTex.rgb;
                     float specPow = lerp(1, _SpecPow, var_SpecTex.a);
                     float phong = pow(max(0.0, vdotr), specPow);
                     //DirTotal
                     float shadow = LIGHT_ATTENUATION(i);
                     float3 dirLighting = (baseCol * lambert + specCol * phong) * _LightColor0 * shadow;
                
                     //EnvDif
                     float upMask = max(0.0, nDirWS.g);          // 获取朝上部分遮罩
                     float downMask = max(0.0, -nDirWS.g);       // 获取朝下部分遮罩
                     float sideMask = 1.0 - upMask - downMask;   // 获取侧面部分遮罩
                     float3 envCol = _EnvUpCol * upMask +
                                 _EnvSideCol * sideMask +
                                 _EnvDownCol * downMask;     // 混合环境色
                     float3 envdif = envCol * baseCol * _EnvDiffInt;
                     //EnvSpe
                     float fresnel = pow(max(0.0, 1.0 - vdotn), _FresnelPow);
                     float3 envSpe = var_Cubemap * fresnel * _EnvSpecInt;    
                     //EnvTotal
                     float occlusion = var_MainTex.a;
                     float3 envLighting = (envdif + envSpe) * occlusion;
                
                     //自发光
                     float emitInt = _EmitInt * (sin(frac(_Time.z)) * 0.5 + 0.5);
                     float3 emission = var_EmitTex * emitInt;
                
                     //LightResult
                     float3 finaRBG = dirLighting + envLighting + emission;
                 //Result
                //return Phong;
                return float4(finaRBG,1.0);

            }
            ENDCG
        }
    }
    FallBack  "Diffuse"
}
```



